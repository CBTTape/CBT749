//GILBERTD JOB (ACCT#),DEFGDGSR,                                 OS/VS2
// NOTIFY=GILBERT,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//ASMXF EXEC PGM=IFOX00,PARM=(OBJECT,NODECK,NOESD,NORLD,ALIGN,NOXREF)
***********************************************************************
*                                                                     *
*        DEFINE GDG                 GSF - OCTOBER 1987                *
*                                                                     *
*        THIS SUBROUTINE MAY BE CALLED BY THE CZXG0DFP PDB EXIT       *
*        TO DEFINE THE GDGS.  IT REQUIRES TWO PARAMETERS:             *
*                                                                     *
*        1. A 44-BYTE FIELD, CONTAINING THE DSNAME WHICH MUST BE      *
*           LEFT-JUSTIFIED AND PADDED WITH BLANKS.                    *
*                                                                     *
*        2. A 3-DIGIT FIELD, CONTAINING THE NUMBER OF GENERATIONS     *
*           AS AN UNPACKED DECIMAL NUMBER IN THE RANGE 001 TO 255.    *
*                                                                     *
*        SAMPLE CALLING SEQUENCE:                                     *
*                                                                     *
*            CALL 'DEFGDG' USING XF-EDMDSNAM, XF-EDMGDGNB.            *
*                                                                     *
*        A RETURN CODE IS PASSED IN REGISTER 15.  A COBOL CALLING     *
*        PROGRAM MAY CHECK THE CONTENT OF THE "RETURN-CODE" FIELD,    *
*        A NON-ZERO VALUE INDICATING THAT AN ERROR HAS OCCURED.       *
*                                                                     *
*                                                                     *
*        THIS SUB-ROUTINE DOESN'T CALL IDCAMS, BUT DIRECTLY           *
*        INVOKES THE MVS CATALOG MANAGEMENT SERVICES TO PERFORM       *
*        THE FOLLOWING:                                               *
*                                                                     *
*        1. LISTCAT THE ENTRY TO CHECK IF THE GDG ALREADY EXISTS      *
*           AND, IF THIS IS THE CASE, WHAT THE CURRENT LIMIT IS.      *
*           THIS REQUEST IS EQUIVALENT TO THE IDCAMS COMMAND:         *
*              'LISTCAT MYDSN GDG ALL '.                              *
*                                                                     *
*        2. DELETE THE EXISTING GDG BASE IF THE LIMIT IS DIFFERENT    *
*           FROM THE PASSED GDGNB.  IF THE GDG BASE IS NOT EMPTY,     *
*           DELETE WILL FAIL AND DEFINE WILL NOT BE ATTEMPTED.        *
*           THIS REQUEST IS EQUIVALENT TO THE IDCAMS COMMAND:         *
*              'DELETE MYDSN GDG '.                                   *
*                                                                     *
*        3. DEFINE THE GDG WITH THE SCRATCH OPTION AND THE NUMBER     *
*           OF GENERATIONS PASSED IN THE SECOND PARAMETER.            *
*           THIS REQUEST IS EQUIVALENT TO THE IDCAMS COMMAND:         *
*              'DEFINE GDG(NAME(MYDSN) LIMIT(GDGNB) SCRATCH)'.        *
*                                                                     *
*        NOTES:                                                       *
*                                                                     *
*        THE PROGRAM IS REUSABLE, WHICH MEANS THAT IT MAY BE LOADED   *
*        ONCE AND CALLED SEVERAL TIMES, AS DURING A TSO SESSION,      *
*        A JOBSET COMPILATION OR A FILE MASS-COMPILATION.             *
*                                                                     *
*        THE PROGRAM DOESN'T DEFINE A GDG IN THE FOLLOWING CASES:     *
*                                                                     *
*        1. A '//NODEFGDG DD DUMMY' STATEMENT IS PRESENT IN THE JCL   *
*           OR THE EQUIVALENT "ALLOCATE" COMMAND HAS BEEN ISSUED      *
*           UNDER TSO.                                                *
*                                                                     *
*        2. THE FIRST LEVEL QUALIFIER OF THE DSNAME HAS NOT BEEN      *
*           DEFINED AS AN ALIAS.                                      *
*                                                                     *
*        3. THE SECOND PARAMETER IS ZERO.  IN THAT CASE, IF A GDG     *
*           BASE EXISTS FOR THAT ENTRY AND IS EMPTY, IT IS DELETED.   *
*                                                                     *
*$200 SET GDG BASE'S OWNER TO INVOKER'S RACF USER ID                  *
***********************************************************************
DEFGDG   CSECT
         SAVE  (14,12),,'GSF UTILITIES - DEFGDGSR R200'
         LR    R12,R15
         USING DEFGDG,R12
         LM    R3,R4,0(R1)             GDGNAME, GDGLIMIT
*LA R3,=CL44'GILBERT.GDG' ###TEST###
*LA R4,=C'032'            ###TEST###
         GETMAIN RU,LV=DYNL            GET DYNAMIC STORAGE AREA
         LR    R9,R1                   KEEP ITS ADDRESS
         USING DYN,R9
         DEVTYPE =C'NODEFGDG',WORK265  CHECK DDNAME
         LTR   R15,R15                 CHECK DEVTYPE RETURN CODE
         BZ    RETURN                  DD PRESENT, QUIT.
         LA    R14,DYN                 CLEAR DYNAMIC STORAGE
         LA    R15,DYNL                CLEAR DYNAMIC STORAGE
         SLR   R1,R1                   CLEAR DYNAMIC STORAGE
         MVCL  R14,R0                  CLEAR DYNAMIC STORAGE
*---------------------------------------------------------------------
*        CHECK FOR AN EXISTING ALIAS
*---------------------------------------------------------------------
P1ALIAS  MVI   WORK265+C'.',C'.'       BUILD TABLE FOR TRT
         TRT   0(9,R3),WORK265         FIND FIRST PERIOD
         BZ    RETURN                  ERROR IF NO PERIOD FOUND
         LR    R0,R3                   POINT TO GDG NAME
         SLR   R1,R0                   COMPUTE LENGTH OF 1ST QUALIFIER
         ICM   R1,B'1000',=C' '        PADDING CHARACTER
         LA    R14,CATNAME             "TO" FIELD
         ST    R14,LOCALIAS+4          UPDATE CAMLST
         ST    R14,CTGCAT              UPDATE CTGPL
         LA    R15,L'CATNAME           LENGTH
         MVCL  R14,R0                  MOVE ALIAS TO CATNAME FIELD
         LA    R0,WORK265              GET ADDRESS OF WORK AREA
         ST    R0,LOCALIAS+12          UPDATE CAMLST
         ST    R0,CTGWKA               UPDATE CTGPL
         LOCATE LOCALIAS               FIND UCAT NAME
         LTR   R15,R15                 CHECK SVC 26 RETURN CODE
         BNZ   RETURN                  NO ALIAS, QUIT.
         PACK  DWD,0(3,R4)             PACK 3-DIGIT GDGLIMIT
         OI    DWD+7,X'0F'             PREVENT S0C7 IF ALL SPACES
         CVB   R0,DWD                  CONVERT TO BINARY, GET S0C7
         STC   R0,GDGLIMIT             KEEP GDG LIMIT
*---------------------------------------------------------------------
*        LISTCAT
*---------------------------------------------------------------------
P2LISTC  ST    R3,CTGENT               GDGNAME
         MVC   WORK265(4),=Y(L'WORK265,0) FORMAT WORK AREA HEADER
         MVI   CTGOPTNS,CTGLTCAT       FUNCTION = LISTCAT
         CATALOG CTGPL                 IS GDG DEFINED ALREADY?
         LTR   R15,R15                 CHECK SVC 26 RETURN CODE
         BNZ   P3ZERO                  GDG ISN'T DEFINED, GO DO IT.
*---------------------------------------------------------------------
*        DELETE GDG IF CURRENT LIMIT IS DIFFERENT FROM THE ONE REQ'D
*---------------------------------------------------------------------
P3DELETE L     R1,CTGFLPT              POINTER TO RETURNED GDGLIMIT
         CLC   GDGLIMIT,0(R1)          COMPARE WITH PASSED VALUE
         BE    RETURN                  GDG OK ALREADY, RETURN.
         MVI   CTGOPTNS,CTGDELET       FUNCTION = DELETE
         CATALOG CTGPL                 DELETE EXISTING GDG
         LTR   R15,R15                 CHECK SVC 26 RETURN CODE
         BNZ   RETURN                  DELETE FAILED, RETURN
P3ZERO   CLI   GDGLIMIT,0              CHECK GDGLIMIT
         BE    RETURN                  QUIT IF GDGLIMIT=0
*---------------------------------------------------------------------
*        RETRIEVE USERID FROM ACEE                                @200
*---------------------------------------------------------------------
         L     R1,PSAAOLD-PSA          MY ASCB
         L     R1,ASCBASXB-ASCB(,R1)   MY ASXB
         L     R1,ASXBSENV-ASXB(,R1)   MY ACEE
         LTR   R1,R1
         BNP   P4DEFINE                NO ACEE, NO OWNER
         MVC   OWNERID,ACEEUSRI-ACEE(R1)  MY RACF USER ID
         LA    R0,CTGFL2               CTGFL FOR OWNER ID
         ST    R0,CTGFV+28             SET POINTER TO OWNER ID
*---------------------------------------------------------------------
*        DEFINE GDG WITH SCRATCH OPTION.
*---------------------------------------------------------------------
P4DEFINE LA    R1,CTGFV                ADDR OF CTGFV
         ST    R1,CTGFVT               STORE CTGFV ADDRESS INTO CTGPL
         MVI   CTGFV+00,C'B'           SET TYPE TO "GDG BASE"
         ST    R3,CTGFV+20             ADDR OF DSNAME
         LA    R14,GDGLIMIT            GDG LIMIT
         LA    R15,GDGATTR             GDG ATTRIBUTES
         STM   R14,R15,CTGFV+72        SET POINTERS TO GDGLIMIT/GDGATTR
         MVI   CTGOPTNS,CTGDEFIN       FUNCTION = DEFINE
         CATALOG CTGPL                 DEFINE GDG
*---------------------------------------------------------------------
RETURN   SAVE  15                      STORE RETURN CODE
         FREEMAIN RU,LV=DYNL,A=(R9)    FREE DYNAMIC STORAGE AREA
         RETURN (14,12)                RETURN, PASS RETURN CODE.
*---------------------------------------------------------------------
*
*        CATALOG MANAGEMENT CONTROL BLOCKS: CTGPL, CTGFL
*
*        FOR INFORMATION ON CATALOG MANAGEMENT REQUESTS AND
*        CONTROL BLOCKS, REFER TO A DFP/370 OR DFP 2.X
*        "DIAGNOSIS REFERENCE" MANUALS.  IF YOU FIND IT IN THE
*        DFP 3.X DOCUMENTATION, PLEASE LET ME KNOW.
*
*---------------------------------------------------------------------
CTGPL    DS    0F
CTGOPTN1 DC    AL1(CTGNAME+CTGCNAME)   FIRST OPTION BYTE:
CTGNAME  EQU   X'04'                     CTGENT POINTS TO ENTRY NAME
CTGCNAME EQU   X'02'                     CTGCAT POINTS TO CATALOG NAME
         DC    B'0'
CTGFUNC  DC    AL1(CTGCMS+CTGAM0)      CALLER-REQUESTED FUNCTION
CTGCMS   EQU   X'80'                     REQUEST IS IN CTGOPTNS
CTGAM0   EQU   X'01'                     OS/VS2 CTGPL, NOT A CAMLST
         DC    B'0'
CTGFVT   DS    0A(*-*)                 ADDRESS OF CTGFV FOR DEFINE
CTGENT   DC    A(*-*)                  ADDRESS OF ENTRY NAME
CTGCAT   DC    A(*-*)                  ADDRESS OF CATALOG DSNAME
CTGWKA   DC    A(*-*)                  ADDRESS OF CALLER'S WORK AREA
CTGOPTNS DC    AL1(CTGLTCAT)           REQUEST:
CTGLTCAT EQU   X'20' 0010 0...           LISTCAT
CTGDELET EQU   X'18' 0001 1...           DELETE
CTGDEFIN EQU   X'08' 0000 1...           DEFINE
         DC    B'0'
CTGTYPE  DC    C'B'                    TYPE OF CAT. RECORD (GDG BASE)
CTGNOFLD DC    FL1'1'                  NUMBER OF POINTERS IN CTGFIELD
CTGFDBK  DC    XL2'0'                  FEEDBACK AREA
CTGFBFLG DC    B'0'
         DC    X'00'                   ERROR CODE
CTGPSWD  DC    A(0)                    PASSWORD ADDRESS
CTGFIELD DC    A(CTGFL)                POINTER(S) TO CTGFL(S)
*
CTGFL    DS    0F                   +0 FIELD DESCRIPTOR
CTGFLDNO DC    FL1'1',5X'00'        +0 NUMBER OF FIELDS
CTGFLDNM DC    A(FLDNM1,0)          +8 PTR TO FIELD NAME
CTGFLNG  DC    F'0'                +16 LENGTH OF RETURNED DATA
CTGFLPT  DC    A(*-*)              +20 ADDRESS OF RETURNED DATA
*
FLDNM1   DC    C'GDGLIMIT'             FIELD NAME
GDGATTR  DC    B'01000000'             GDG ATTRIBUTES: SCRATCH, NOEMPTY
GDGLIMIT DC    FL1'0'                  BINARY GDG LIMIT
LOCALIAS CAMLST NAME,*-*,,*-*          LOCATE ALIAS
*
CTGFL2   DS    0F                   +0 FIELD DESCRIPTOR
         DC    FL1'1',5X'00'        +0 NUMBER OF FIELDS
         DC    A(FLDNM2,0)          +8 PTR TO FIELD NAME
         DC    F'8'                +16 LENGTH OF RETURNED DATA
CTGFL2PT DC    A(OWNERID)          +20 ADDRESS OF RETURNED DATA
*
FLDNM2   DC    C'OWNERID '             FIELD NAME
OWNERID  DC    C'XXXXXXXX'             VALUE
*---------------------------------------------------------------------
*        DYNAMIC STORAGE, BASED WITH R9.
*---------------------------------------------------------------------
DYN      DSECT
CATNAME  DS    CL44' '                 CATALOG DSNAME
CTGFV    DS    32F                     128-BYTE CTGFV
DWD      DS    D                       WORK
WORK265  DS    XL265                   WORK AREA FOR LISTCAT, DELETE.
DYNL     EQU   *-DYN
         PRINT NOGEN
         IHAPSA DSECT=YES
         IHAASCB DSECT=YES
         IHAASXB
         IHAACEE
*        YREGS                         SYS1.MACLIB
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,15)
//SYSUT2   DD UNIT=VIO,SPACE=(CYL,15)
//SYSUT3   DD UNIT=VIO,SPACE=(CYL,15)
//SYSGO    DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3120
//SYSPRINT DD SYSOUT=*
//*
//LKED    EXEC PGM=IEWL,PARM=REUS
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD DSN=*.ASMXF.SYSGO,DISP=(OLD,PASS)
//SYSLMOD  DD DSN=GILBERT.LOAD4(DEFGDGSR),DISP=SHR
//*
//GO      EXEC PGM=LOADER,PARM=NOPRINT
//SYSLIN   DD DSN=*.ASMXF.SYSGO,DISP=(OLD,PASS)
//SYSUDUMP DD SYSOUT=*
//*
//AMS     EXEC PGM=IDCAMS
//SYSPRINT DD SYSOUT=*
 LISTC ENT('GILBERT.GDG') ALL
/* DELETE    'GILBERT.GDG'
