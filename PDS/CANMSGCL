//GILBERTC JOB (ACCT#),CANMSGCL,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//ASMXF EXEC PGM=IFOX00,PARM=(OBJECT,NODECK,NOESD,NORLD,ALIGN,NOXREF)
***********************************************************************
*                                                                     *
*              CANCEL JOB'S MSGCLASS AFTER NN HOURS                   *
*                                                                     *
***********************************************************************
*                                                                     *
*        THIS PROGRAM ALLOWS YOU TO PURGE THE OUTPUT ASSOCIATED       *
*        WITH THE MSGCLASS OF THE JOB AFTER A CERTAIN NUMBER OF       *
*        HOURS THAT YOU SPECIFY IN THE PARM FIELD OF THE EXEC         *
*        STATEMENT.                                                   *
*                                                                     *
*        EXAMPLE:                                                     *
*                                                                     *
*            //MYJOB    JOB ACCT#,CLASS=A,MSGCLASS=X                  *
*            //COPY1   EXEC PGM=IEBCOPY                               *
*            //...      DD   ...                                      *
*            //CONDPURG EXEC PGM=CANMSGCL,PARM=2,COND=(0,NE,COPY1)    *
*                                                                     *
*        IF THE 'COPY1' STEP ENDS AT 10:28 AM WITH A RETURN CODE      *
*        EQUAL TO ZERO, THE 'CONDPURG' STEP EXECUTES AND ISSUES       *
*        THE FOLLOWING COMMAND:                                       *
*                                                                     *
*               $TA,T=12.28,'$OJ01234,Q=X,CANCEL'                     *
*                                                                     *
*        NOTES:                                                       *
*                                                                     *
*           1. THE PROGRAM MUST BE APF-AUTHORIZED.                    *
*                                                                     *
*           2. THE MSGCLASS MUST BE HELD.                             *
*                                                                     *
*           3. THE NUMBER OF OUTSTANDING AUTOMATIC COMMANDS IS        *
*              LIMITED BY THE 'AUTOCMD' PARAMETER IN JES2PARM.        *
*              IF EXCEEDED, THE COMMAND IS IGNORED.                   *
*                                                                     *
*           4. THE PROGRAM WORKS WITH 5-DIGIT JOB NUMBERS.            *
*                                                                     *
***********************************************************************
CANMSGCL CSECT
         SAVE  (14,12),,'GSF UTILITIES - CANMSGCL R107'
         BALR  R12,0
         USING *,R12
         L     R2,0(,R1)               PARM ADDRESS
         LH    R14,0(,R2)              PARM LENGTH
         BCTR  R14,0
         PACK  DELAY,2(*-*,R2)         PACK NUMBER OF HOURS
         EX    R14,*-6                 EXECUTE PACK INSTRUCTION
*
*        GET JOB NUMBER FROM SSIB
*
         L     R4,540(,0)              PSATOLD
         L     R5,TCBJSCB-TCB(,R4)     PTR TO JSCB
         USING IEZJSCB,R5
         L     R6,JSCBSSIB             PTR TO SSIB
         USING SSIB,R6
         MVC   JOBID+0(1),SSIBJBID+0   J
         MVC   JOBID+1(5),SSIBJBID+3    01234
         L     R8,JSCBJCT              PTR TO JCT
         USING JCT,R8
         MVC   MSGCLASS,JCTJMGPO       MSGCLASS
         DROP  R5,R6,R8                JSCB, SSIB, JCT
*
*        GET CURRENT TIME, MOVE IT TO "HHMM" FIELD
*
         TIME  DEC                     HHMMSS00
         SRL   R0,12(0)                000HHMM0
         ST    R0,SYSTIME              000HHMM0
         OI    SYSTIME+3,15            000HHMMF
         UNPK  HHMM+3(2),SYSTIME+2(2)  MOVE MM
         OI    SYSTIME+2,15            000HHF??
         AP    SYSTIME+1(2),DELAY      ADD HOURS FROM PARM
         OI    SYSTIME+2,15            000HHF??
         UNPK  HHMM+0(2),SYSTIME+1(2)  MOVE HH
         MVI   HHMM+2,C'.'             HH.MM
         MODESET KEY=ZERO,MODE=SUP
         SLR   R0,R0                   DUMMY CONSOLE ID
         MGCR  MGCR1L                  ISSUE COMMAND
         RETURN  (14,12),RC=00         GO BACK TO SYSTEM
*
MGCR1L   WTO   '$TA,T=HH.MM,''$OJ01234,Q=$,CANCEL''',MF=L
HHMM     EQU   MGCR1L+10               HH.MM
JOBID    EQU   MGCR1L+19,6             JOB ID
MSGCLASS EQU   MGCR1L+28,1             MESSAGE CLASS
DELAY    DS    P'24'                   NUMBER OF HOURS, FROM PARM
SYSTIME  DS    F                       CURRENT SYSTEM TIME
*
         IKJTCB LIST=NO                TCB DSECT
*
         IEZJSCB                       JSCB DSECT
*
JCT      DSECT
         DS    4F                      JCT PREFIX
         IEFAJCTB                      JCT DSECT
*
         IEFJSSIB                      SSIB DSECT
*
*        YREGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,15)
//SYSUT2   DD UNIT=VIO,SPACE=(CYL,15)
//SYSUT3   DD UNIT=VIO,SPACE=(CYL,15)
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.AMODGEN,DISP=SHR
//SYSGO    DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3120
//SYSPRINT DD SYSOUT=*
//*
//LKED    EXEC PGM=HEWL,PARM='MAP,REUS,AC=1'
//SYSLIN   DD DSN=*.ASMXF.SYSGO,DISP=(OLD,DELETE)
//SYSPRINT DD SYSOUT=*
//SYSLMOD  DD DSN=SYS2.AUTHLIB(CANMSGCL),DISP=SHR
//*
//GO      EXEC PGM=CANMSGCL,PARM=2
//STEPLIB  DD DSN=SYS2.AUTHLIB,DISP=SHR
//SYSUDUMP DD SYSOUT=*
//ABNLNODP DD DUMMY
