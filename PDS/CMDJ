//GILBERTC JOB (ACCT#),CMDJ,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//ASMXF EXEC PGM=IFOX00,PARM=(OBJECT,NODECK,NOESD,NORLD,ALIGN,NOXREF)
***********************************************************************
*                                                                     *
*        SEND A JES2 COMMAND FOR CURRENT JOB                          *
*                                                                     *
*        EXAMPLE:                                                     *
*                                                                     *
*           //PURGE   EXEC PGM=CMDJES2,PARM=P       PURGE THE JOB     *
*                                                                     *
*           SENDS A $PJ#### COMMAND TO JES2, WHERE #### IS THE        *
*           JOB'S OWN JOB NUMBER.                                     *
*                                                                     *
***********************************************************************
CMDJES2  CSECT
         SAVE  (14,12),,'GSF UTILITIES - CMDJ R101'
         LR    R12,R15
         USING CMDJES2,R12
         L     R10,0(,R1)           PARM ADDRESS
         LH    R14,0(,R10)          PARM LENGTH
         CH    R14,=Y(L'CMD1CMD)    TOO LONG?
         BH    RC12                 YES, QUIT
         BCTR  R14,0
         EX    R14,*+4
         MVC   CMD1CMD(*-*),2(R10)  MOVE COMMAND
         L     R3,X'21C'            PSATOLD (CURRENT TCB)
         L     R4,X'0B4'(,R3)       JSCB
         L     R5,X'13C'(,R4)       SSIB
         MVC   CMD1JOBI,X'00C'(R5)  SSIBJBID=JOB01234
         MVI   CMD1JOBI+1,C' '      O
         MVI   CMD1JOBI+2,C' '      B
         MODESET KEY=ZERO,MODE=SUP
         SLR   R0,R0
         MGCR  CMD1              <- ISSUE $P CMD
         MODESET KEY=NZERO,MODE=PROB
         STIMER WAIT,BINTVL==A(500) WAIT 5 SECONDS
         RETURN (14,12),RC=0
RC12     RETURN (14,12),RC=12       PARM IS TOO LONG
CMD1     WTO   '$                                     ',MF=L
CMD1CMD  EQU   CMD1+5,24   P
CMD1JOBI EQU   CMD1+6,8    JOB03210
*        YREGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,15)
//SYSUT2   DD UNIT=VIO,SPACE=(CYL,15)
//SYSUT3   DD UNIT=VIO,SPACE=(CYL,15)
//SYSGO    DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3120
//SYSPRINT DD SYSOUT=*
//*
//LKED    EXEC PGM=IEWL,PARM='MAP,AC=1'
//SYSLIN   DD DSN=*.ASMXF.SYSGO,DISP=(OLD,DELETE)
//SYSPRINT DD SYSOUT=*
//SYSLMOD  DD DSN=SYS1.P390.LINKLIB(CMDJES2),DISP=SHR
//*
//GO      EXEC PGM=CMDJES2,PARM=P                     PURGE THE JOB
//STEPLIB  DD DSN=SYS1.P390.LINKLIB,DISP=SHR
